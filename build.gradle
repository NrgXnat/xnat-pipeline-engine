/*
 * pipeline: build.gradle
 * XNAT http://www.xnat.org
 * Copyright (c) 2018, Washington University School of Medicine
 * All Rights Reserved
 *
 * Released under the Simplified BSD.
 */

import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens
import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id "java"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
}

group "org.nrg.xnat"
version = "1.7.7-SNAPSHOT"

def id = "xnat-pipeline-${version}"

repositories {
    mavenLocal()
    maven { url "http://maven.imagej.net/content/groups/public" }
    jcenter()
    mavenCentral()
    maven { url "https://nrgxnat.jfrog.io/nrgxnat/libs-release" }
    maven { url "https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot" }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

String propertyWithDefault(String name, Object value) {
    hasProperty(name) ? property(name) : value
}

// Marshall all of the variables and properties.
def isWindows = Os.isFamily(Os.FAMILY_WINDOWS)

def source = buildDir.parent
def destination = file(propertyWithDefault("destination", "${buildFile.parent}/build/pipeline"))
def url = "${propertyWithDefault("xnatUrl", "http://localhost:8080").replaceAll("/+\$", "")}" as String
def email = "${propertyWithDefault("adminEmail", "admin@yourxnat.org")}" as String
def smtp = "${propertyWithDefault("smtpServer", "localhost")}" as String
def siteName = "${propertyWithDefault("siteName", "XNAT")}" as String
def pluginsDir = "${propertyWithDefault("pluginsDir", "")}" as String
def pluginsDirProd = "${propertyWithDefault("pluginsDirProd", "")}" as String

println """Building pipeline engine

 * From:        ${source}
 * To:          ${destination}
 * Email:       ${email}
 * Site name:   ${siteName}
 * Site URL:    ${url}
 * SMTP server: ${smtp}
 * Plugins dir: ${pluginsDir}
 * Plugins dir (prod): ${pluginsDirProd}
"""

def configExt = (isWindows ? "bat" : "config") as String
def demoScript = (isWindows ? "demo.bat" : "demo.sh") as String
def envVar1 = (isWindows ? "%" : "\$") as String
def envVar2 = (isWindows ? "%" : "") as String
def classpathSeparator = (isWindows ? ";" : ":") as String
def osArgSym = (isWindows ? "%*" : "\$@") as String
def platformCall = (isWindows ? "call" : "source") as String
def shellLine = (isWindows ? "@echo off" : "#!/bin/bash") as String

def pluginBeansJars = []
if ("${pluginsDir}" != "") {
    fileTree(dir: "${pluginsDir}", include: '**/*beans*.jar').each { File file ->
        if ("${pluginsDirProd}" != "") {
            pluginBeansJars.add file.path.replace("${pluginsDir}","${pluginsDirProd}")
        } else {
            pluginBeansJars.add file.path
        }
    }
}

def pluginsBeans = pluginBeansJars.size() > 0 ? classpathSeparator + pluginBeansJars.join(classpathSeparator) : ""

def tokens = [CONFIG_EXT           : configExt,
              CP_SEP               : classpathSeparator,
              OS_ARG_SYM           : osArgSym,
              PIPELINE_DIR_PATH    : "${destination}" as String,
              PLATFORM_CALL        : platformCall,
              PLATFORM_ENV_VAR1    : envVar1,
              PLATFORM_ENV_VAR2    : envVar2,
              SCRIPT_NAME          : demoScript,
              SHELL_LINE           : shellLine,
              XNAT_SITE_NAME       : siteName,
              YOUR_EMAIL_HERE      : email,
              YOUR_SMTP_SERVER_HERE: smtp,
              YOUR_XNAT_URL_HERE   : url,
              PLUGIN_BEANS         : pluginsBeans]

// Now get all of the script and resource folders.
def scripts = file("${source}/templates/scripts").exists() ? [file("${source}/templates/scripts")] : []
def resources = file("${source}/templates/resources").exists() ? [file("${source}/templates/resources")] : []
def binaries = file("${source}/templates/bin").exists() ? [file("${source}/templates/bin")] : []

def moduleRoots = file("${source}/modules").exists() ? [file("${source}/modules")] : []
if (hasProperty("modulePaths")) {
    "${modulePaths}".split(/\s*,\s*/).each { modulePath ->
        def path = file(modulePath)
        if (path.exists()) {
            println "Adding module root ${path}"
            moduleRoots.add path
        } else {
            println "The suggested module root ${path} does not exist, skipping."
        }
    }
}
println ""
moduleRoots.each { root ->
    root.eachDirMatch(~/^(?!\.).*/) { File module ->
        def folders = []
        module.eachDirMatch(~/resources/) { match ->
            folders.add match
        }
        module.eachDirMatch(~/scripts/) { match ->
            folders.add match
        }
        module.eachDirMatch(~/bin/) { match ->
            folders.add match
        }
        if (folders.size() == 0) {
            resources.add module
        } else {
            folders.each { File folder ->
                if (folder.name.endsWith("resources")) {
                    resources.add folder
                } else if (folder.name.endsWith("scripts")) {
                    scripts.add folder
                } else if (folder.name.endsWith("bin")) {
                    binaries.add folder
                }
            }
        }
    }
}

println "Resource folders:"
resources.each { resource ->
    println " * ${resource.path}"
}
println "Script folders:"
scripts.each { script ->
    println " * ${script.path}"
}
println "Binary folders:"
binaries.each { binary ->
    println " * ${binary.path}"
}
println ""

dependencyManagement.imports {
    mavenBom("org.nrg:parent:${project.version}") {
        bomProperties([
                "commons-logging.version": "1.2",
                "httpcomponents-client.version": "4.3.3",
                "javassist.version": "3.20.0-GA"
        ])
    }
}
tokens.putAll(dependencyManagement.managedVersions)

dependencies {
    compile("org.nrg.xnat:data-client") { transitive = false }
    compile("org.nrg.xnat:xnat-data-models") { transitive = false }
    compile("org.nrg.xdat:core") { transitive = false }
    compile("org.nrg:framework") { transitive = false }
    compile("org.nrg:mail") { transitive = false }
    compile("org.nrg:pipelineBase") { transitive = false }
    compile("org.nrg:pipelineWorkflow") { transitive = false }
    compile("org.nrg:pipelineXNAT") { transitive = false }
    compile("org.nrg.xnat.pipeline:pipeline-client") { transitive = false }
    compile("org.nrg.xnat.pipeline:xnat-pipeline") { transitive = false }
    compile("org.nrg.xnat.pipeline:xnat-tools") { transitive = false }
    compile("org.nrg.xnat:plexiviewer") { transitive = false }
    compile("org.nrg.xnat.pipeline:imagingtools") { transitive = false }
    compile("org.nrg.xnat:xnatsrb") { transitive = false }
    compile("org.nrg:DicomImageUtils") { transitive = false }
    compile("org.springframework:spring-web") { transitive = false }
    compile("org.springframework:spring-core") { transitive = false }
    compile("org.springframework:spring-aop") { transitive = false }
    compile("org.springframework:spring-beans") { transitive = false }
    compile("org.springframework:spring-context") { transitive = false }
    compile("commons-configuration:commons-configuration") { transitive = false }
    compile("commons-collections:commons-collections") { transitive = false }
    compile("commons-discovery:commons-discovery") { transitive = false }
    compile("commons-httpclient:commons-httpclient") { transitive = false }
    compile("commons-io:commons-io") { transitive = false }
    compile("commons-lang:commons-lang") { transitive = false }
    compile("commons-beanutils:commons-beanutils") { transitive = false }
    compile("commons-codec:commons-codec") { transitive = false }
    compile("commons-logging:commons-logging") { transitive = false }
    compile("org.apache.commons:commons-email") { transitive = false }
    compile("org.apache.commons:commons-lang3") { transitive = false }
    compile("org.apache.commons:commons-text") { transitive = false }
    compile("org.apache.httpcomponents:fluent-hc") { transitive = false }
    compile("com.fasterxml.jackson.core:jackson-annotations") { transitive = false }
    compile("com.fasterxml.jackson.core:jackson-core") { transitive = false }
    compile("com.fasterxml.jackson.core:jackson-databind") { transitive = false }
    compile("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml") { transitive = false }
    compile("com.fasterxml.jackson.datatype:jackson-datatype-hibernate4") { transitive = false }
    compile("org.apache.ant:ant") { transitive = false }
    compile("org.nrg:config") { transitive = false }
    compile("org.nrg:notify") { transitive = false }
    compile("org.nrg:prefs") { transitive = false }
    compile("org.springframework.security:spring-security-core") { transitive = false }
    compile("com.google.guava:guava") { transitive = false }
    compile("org.apache.httpcomponents:httpcore") { transitive = false }
    compile("org.apache.httpcomponents:httpclient") { transitive = false }
    compile("org.javassist:javassist") { transitive = false }
    compile("org.dspace:jargon") { transitive = false }
    compile("javax.xml:jaxrpc-api") { transitive = false }
    compile("xalan:xalan") { transitive = false }
    compile("javax.activation:activation") { transitive = false }
    compile("axis:axis") { transitive = false }
    compile("aopalliance:aopalliance") { transitive = false }
    compile("org.reflections:reflections") { transitive = false }
    compile("org.slf4j:slf4j-api") { transitive = false }
    compile("org.slf4j:slf4j-log4j12") { transitive = false }
    compile("org.slf4j:jul-to-slf4j") { transitive = false }
    compile("log4j:log4j") { transitive = false }

    // These are old deprecated libraries that we"re carrying so we don"t break existing functionality.
    compile("ant:ant:1.6.1") { transitive = false }
    compile("com.jcraft:jsch:0.1.24") { transitive = false }
    compile("commons-dbcp:commons-dbcp:1.2.1") { transitive = false }
    compile("commons-pool:commons-pool:1.2") { transitive = false }
    compile("javax.mail:mail:1.4.5") { transitive = false }
    compile("net.sf.ezmorph:ezmorph:1.0.6") { transitive = false }
    compile("net.sf.saxon:Saxon-B:9.0") { transitive = false }
    compile("org.apache.httpcomponents:fluent-hc:4.2.3") { transitive = false }
    compile("velocity:velocity:1.3.1") { transitive = false }
}

// Pulls in the Jenkins BUILD_NUMBER environment variable if available.
def buildDate = new Date()
def buildNumber = System.getenv().BUILD_NUMBER?.toInteger() ?: "Manual"

logger.info "Build-Date:             ${buildDate}"
logger.info "Build-Number:           ${buildNumber}"
logger.info "Implementation-Version: ${version}"

task buildArchive(type: Zip) {
    manifest {
        doFirst {
            delete fileTree(dir: projectDir, include: "*.mf", exclude: "${id}.mf")
        }
        attributes "Application-Name": "XNAT Pipeline Engine ${project.version}",
                "Build-Date": buildDate,
                "Build-Number": buildNumber,
                "Implementation-Version": version
        writeTo "${projectDir}/${id}.mf"
    }

    from("${projectDir}") {
        into "xnat-pipeline"
    }
    exclude "build/**"
    exclude "gradle.properties"
    exclude ".*"
    exclude ".*/**"
    archiveName "${id}.zip"
    destinationDir(file("${buildDir}/libs"))
}

task copyDependencies(type: Copy) {
    from configurations.runtime
    into "${destination}/lib"
}

task copyResources(type: Copy) {
    from resources
    into destination
    exclude ".*/**"
    filter(FixCrLfFilter)
    filter(ReplaceTokens, tokens: tokens)
}

task copyScripts(type: Copy) {
    from scripts
    into destination
    exclude ".*/**"
    fileMode = 0755
    rename { String filename ->
        if (isWindows) {
            "${filename}.bat" as String
        } else {
            filename
        }
    }
    filter(FixCrLfFilter)
    filter(ReplaceTokens, tokens: tokens)
}

task copyBinaries(type: Copy) {
    from binaries
    into destination
    exclude ".*/**"
    fileMode = 0755
}

task copyLibs(type: Copy) {
    from "${source}/lib"
    into "${destination}/lib"
    exclude ".*/**"
}

task copyBuildInfo(type: Copy) {
    from "${source}"
    into "${destination}"
    include "${id}.mf"
}

task copyAll {
    dependsOn tasks.matching { Task task ->
        task instanceof Copy && task.name != "createDist"
    }
}

defaultTasks "copyAll"
